# -*- cperl -*-

#------------------------------------------------------------------------

=item public void writeConfigFile( RunBuilder $rb, ParameterMask $mask, { ClusterInput => string } $cluster_inputs );

Write the config file for this component in support of an Ergatis run.

=item public void writeConfigFile( RunBuilder $rb, ParameterMask $mask, { ClusterInput => string } $cluster_inputs, int $iteration );

Write the $iteration-th config file for this component in support of an Ergatis run.

=cut
#------------------------------------------------------------------------
  sub writeConfigFile {

    my ($self, $rb, $mask, $cluster_inputs, $iteration) = @_;

    # register RunBuilder
    $self->{run_builder} = $rb;
    $self->{cluster_inputs} = $cluster_inputs;
    $iteration and $self->{iteration} = $iteration;

    # do we copy our parameter mask from a different component
    my $build_component = $self->getCopyParameterMask || $self;

    if ( my $builder = ISGA::ComponentBuilder->new($build_component, $mask) ) {
      foreach ( @{$builder->getParameters} ) {
	
	my $command = '';
	my $value = $_->{VALUE};
	
	# if the parameter uses a command line flag, supply it here
	if ( defined $value ) {
	  $command = exists $_->{FLAG} ? "$_->{FLAG} $value" : "$value";
	}
	
	# add separating space to command if it already has a value
	if ( defined $self->{parameters}{$_->{CONFIGLINE}} ) {
	  $self->{parameters}{$_->{CONFIGLINE}} .= " $command";
	} else {
	  $self->{parameters}{$_->{CONFIGLINE}} = $command;
	}
      }
    }

    # use Mason to generate the config file
    my $out_buffer;
    my $interp = HTML::Mason::Interp->new( comp_root  => '___package_include___', out_method => \$out_buffer );
    $interp->exec( $self->getConfigFilePath, self => $self );

    # write the file
    my $path = $rb->getFullErgatisPath . '/'. $self->getErgatisName . '.config';
    ISGA::Utility->writeFile($path, $out_buffer);
  }
  
#------------------------------------------------------------------------

=item public string getParameter(string $name);

Returns a the ergatis config value for the supplied parameter.

=cut

#------------------------------------------------------------------------
  sub getParameter {
    
    my ($self, $name) = @_;
    exists $self->{run_builder} or X::API::ComponentNotConfigured->throw();
    exists $self->{parameters}{$name} or X::API->throw( message => "$name not found as parameter for component " . $self->getName );
    return $self->{parameters}{$name};
  }

#------------------------------------------------------------------------

=item public string getIteration();

Returns the iteration count for the component.

=cut

#------------------------------------------------------------------------
  sub getIteration {

    my $self = shift;
    exists $self->{iteration} or X::API::ComponentNotConfigured->throw();
    return $self->{iteration};
  }
    
#------------------------------------------------------------------------

=item public string getInput(string $name);

Returns a the ergatis config value for the supplied input.

=cut

#------------------------------------------------------------------------
  sub getInput {
    
    my ($self, $name) = @_;

    exists $self->{run_builder} or X::API::ComponentNotConfigured->throw();

    my $ci = ISGA::ClusterInput->new( Component => $self, Name => $name );
    my $value = exists $self->{cluster_inputs}{$ci} ? $self->{cluster_inputs}{$ci} : $ci->getValue($self->{run_builder});
    return $value;
  }

#------------------------------------------------------------------------

=item public string getProjectIDRoot();

Returns a cloned ComponentBuilder with ParameterMask values injected in

=cut

#------------------------------------------------------------------------
  sub getProjectIDRoot {

    my $self = shift;

    exists $self->{run_builder} or X::API::ComponentNotConfigured->throw();

    return $self->{run_builder}->getScrubbedName;
  }



#------------------------------------------------------------------------

=item public ISGA::ComponentBuilder injectMaskValues(ParameterMask $mask, ComponentBuilder $cb);

Returns a cloned ComponentBuilder with ParameterMask values injected in

=cut

#------------------------------------------------------------------------
  sub injectMaskValues{

    my ($self, $parameter_mask, $cb) = @_;
    
    my $mask_params = exists $parameter_mask->{Component}->{$self} ?
      $parameter_mask->{Component}->{$self} : undef;
    
    foreach (keys %$mask_params){
#      $cb->{ParameterLookup}{$_}{VALUE} = $mask_params->{$_}->{Value};
      $cb->{ParameterLookup}{$_}{VALUE} = ref $mask_params->{$_}->{Value} eq 'ARRAY' ? $self->formatArrayParam($mask_params->{$_}->{Value}) : $mask_params->{$_}->{Value};
      $cb->{ParameterLookup}{$_}{ANNOTATION} = $mask_params->{$_}->{Description};
    }

    return $cb;
  }

#------------------------------------------------------------------------

=item public ISGA::Component formatArrayParam(ParameterMaskValue $array);

Formats an array of values for a component input value.

=cut

#------------------------------------------------------------------------
  sub formatArrayParam{
   my ($self, $array);
     return $array;
  }
