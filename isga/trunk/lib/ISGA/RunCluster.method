# -*- cperl -*-

use List::Util qw( min reduce sum );

use File::Path qw( remove_tree );

use IPC::Run qw(run); 
use Cwd;

# initialize status rank.  Lower number is higher priority
my %status_rank = ( "error" => 1,
		    "failed" => 2,
		    "interrupted" => 3,
		    "running" => 4,
		    "incomplete" => 5,
		    "complete" => 6,
		  );

my %status_reverse = ( 1 => 'Error',
		       2 => 'Failed',
		       3 => 'Interrupted',
		       4 => 'Running',
		       5 => 'Incomplete',
		       6 => 'Complete',
		       7 => 'Incomplete',
		     );

#------------------------------------------------------------------------

=item public void purge();

Removes the ergatis directories for this run cluster.

=cut
#------------------------------------------------------------------------
sub purge {

  my $self = shift;

  my $run = $self->getRun;
  my $id = $run->getErgatisKey;
  
  foreach my $component ( @{$run_cluster->getComponents()} ) {

    # grab ergatis name
    my $e_name = $_->getErgatisName;
    my $e_label = $_->getErgatisLabel;

    # nuke output
    my $out_dir = join( '/', $run->getType->getOutputRepository, $e_name, "$id_$e_label"); 

    remove_tree($out_dir);

    # nuke working dir
    my $work_dir = join( '/', $run->getErgatisProjectDirectory, 'workflow/runtime', $e_name, "$id_e_label");
    
    remove_tree($work_dir);
  }
  
}

#------------------------------------------------------------------------

=item public void updateStatus(hashref $ergatis);


This will update the run cluster status, as well as pull information
for outputs, using the supplied hash ref summarizing the pipeline.xml
output.

=cut
#------------------------------------------------------------------------
sub updateStatus {

  my ($self, $ergatis, $components) = @_;

  # hash to store changes to runcluster object
  my %edit;
  
  # to start with we are off the charts
  my $key = 7;
  
  my $run = $self->getRun;

  # don't process complete clusters
  my $status = $self->getStatus->getName;
  return if $status eq 'Complete';
  
  my @component_names = grep { exists $ergatis->{$_} } map { $_->getErgatisName } @$components;

  # go through each component in a cluster and take the lowest number
  $key = min( $key, $status_rank{ $ergatis->{$_}{state} } ) for @component_names;

  # if we've started the cluster since the last update, grab the start time
  if ( $status eq 'Incomplete' and $key != 5 ) {
    
    my @timestamps;
    
    foreach ( grep { exists $ergatis->{$_}->{startTime} } @component_names ) {
      push @timestamps, ISGA::Timestamp->new( $ergatis->{$_}->{startTime}  );
    }

    $edit{ StartedAt } = reduce { $a < $b ? $a : $b } @timestamps;
  }
  
  # if we've finished the cluster since the last update, grab the end time
  if ( $key == 6 ) {
    
    my @timestamps;
    
    foreach ( grep { exists $ergatis->{$_}->{endTime} } @component_names ) {
      push @timestamps, ISGA::Timestamp->new( $ergatis->{$_}->{endTime}  );
    }   
    
    $edit{ FinishedAt } = reduce { $a > $b ? $a : $b } @timestamps;
  }
 
  # if we've started the component, calculate the steps
  if ( $key != 5 ) {

    $edit{FinishedActions} = 
      sum map { $ergatis->{$_}{complete} || 0 } @component_names;
    
    $edit{TotalActions} = 
      sum map { $ergatis->{$_}{total} || 0 } @component_names;
  }
  
  # update status if it has changed
  if ( $status ne $status_reverse{ $key } ) {

    $edit{Status} = ISGA::RunStatus->new( Name => $status_reverse{ $key } );
  }

  # if it's complete we want to get all of the output
  if ( $key == 6 ) {
    
    # find the outputs of this cluster
    foreach my $cout ( @{ISGA::ClusterOutput->query( Component => $components )} ) {
      next if $cout->getVisibility eq 'Internal';

      # find the outputs of this cluster
      foreach ( @{ISGA::RunOutput->query( ClusterOutput => $cout, Run => $run )} ) {
	next if $_->getFileResource;

	# sometimes ergatis will list a component as finished before all the output has been
	# finalized. When this happens, we don't set the status to complete so that the 
	# next pass can pick it up.
	eval { $_->saveErgatisOutput() };
	  
	if ( $@ ) {
	  if ( X::File::NotFound->caught() ) {
	    delete $edit{Status};
	  } else {
	    my $e = X->caught() || X::Dropped->new( message => $@ );
	    $e->rethrow();
	  }
	}

      }      
    }

  }

  # only edit if something has changed
  keys %edit and  $self->edit(%edit);
}

#------------------------------------------------------------------------

=item public boolean hasEvidence();

Returns true if the cluster has evidence files to download.

=cut
#------------------------------------------------------------------------
sub hasEvidence {

  my $self = shift;

  my $cluster = $self->getCluster;
  my $run = $self->getRun;

  foreach ( grep { $_->getCluster == $cluster } @{$run->getType->getComponents} ) {
    ISGA::ClusterOutput->exists( Component => $_, Visibility => 'Evidence' ) and return 1;
  }

  return 0;
}

#------------------------------------------------------------------------

=item public string getEvidenceDownloadName();

Calculate the name for the evidence download for this RunCluster.

=cut
#------------------------------------------------------------------------
sub getEvidenceDownloadName {

  my $self = shift;

  return 'Run-' . $self->getRun->getErgatisKey . ' ' . $self->getCluster->getName . '.tar.gz';
}
