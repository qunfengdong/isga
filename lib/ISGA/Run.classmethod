# -*- cperl -*-

use LWP::UserAgent;
use File::Copy;

#------------------------------------------------------------------------

=item public Run submit(RunBuilder $builder);

Submits a run to ergatis. This method will create a directory in
ergatis for the new run, copy input files to there, write a
pipelinelayout.xml file and config files for each component. The
method then submits the run to ergatis over http.

=cut 
#------------------------------------------------------------------------
sub submit {

  my ($class, $builder) = @_;

  my $pipeline = $builder->getPipeline;

  # retrieve the unique name for this ergatis run directory and create it.
  my $dir = '___ergatis_submit_directory___/' . $builder->getErgatisDirectory;
  mkdir( $dir ) or X::File->throw( error => "Error creating directory $dir: $!" );

  # retrieve the specific inputs for this file
  my $inputs = ISGA::RunBuilderInput->query( RunBuilder => $builder);

  # copy input files to ergatis submit directory
  $_->stage($dir) for map { $_->getFileResource } @$inputs;

  # write config files and layout.xml to ergatis submit directory
  $pipeline->stage($dir, $builder->getParameterMask, $inputs);

  # submit job
  my $id = $class->_submitToErgatis($builder->getErgatisDirectory);

  # make sure we got a run id
  if ( $id !~ m{\A\d+\z} ) {
   
    # set recipients
    my $recipient = ___ERRORNOTIFICATIONEMAIL___;

    if ( ref $recipient ) {
      $recipient = join ( ',', @$recipient );
    }
      

    # send special email
    my %mail =
      ( To => $recipient,
	FROM => '___mail_sender___',
        Subject => 'Ergatis Submission Error',
        Message => "Run Builder $builder Failed:\n$id"
      );
    
    Mail::Sendmail::sendmail(%mail) 
	or X::Mail::Send->throw( text => $Mail::Sendmail::error, message => \%mail );    
    
    # log error
    X::Ergatis::Submit->throw( message => "Illegal Ergatis id: $id" );
    
  }

  # create a file collection
  my $collection = ISGA::FileCollection->create
    ( CreatedBy => ISGA::Login->getAccount,
      ExistsOutsideCollection => 1,
      Type => ISGA::FileCollectionType->new( Name => 'Run Results' ),
      CreatedAt => ISGA::Timestamp->new,
      UserName => $builder->getName . ' Results',
      Description => 'Result files for the Run ' . $builder->getName,
      IsHidden => 0 );

  # create run
  my $run =  ISGA::Run->create
    ( CreatedBy => ISGA::Login->getAccount, Type => $pipeline, 
      FileCollection => $collection,
      RawDataStatus => 'Available',
      Status => ISGA::RunStatus->new( Name => 'Running' ),
      Name => $builder->getName,
      Description => $builder->getDescription,
      CreatedAt => ISGA::Timestamp->new,
      IsHidden => 0, 
      ErgatisKey => $id );

  # log the inputs
  $run->addFileResource( $_->getFileResource ) for @$inputs;

  # prepare the outputs
  $run->_initializeOutputs();

  # prepare the clusters
  $run->_initializeClusters();

  return $run;
}

#------------------------------------------------------------------------

=item PRIVATE int _submitToErgatis(string $dir);

Submits a pipeline template directory to ergatis and returns the new pipeline id.

=cut 
#------------------------------------------------------------------------
sub _submitToErgatis {

  my ($class, $dir) = @_;

  my $ua = LWP::UserAgent->new;

  # set credentials for 
#  $ua->credentials("", "", "", "");

  my $response = 
    $ua->get("___ergatis_submit_uri___?build_directory=$dir&project=___ergatis_project___");

  $response->is_success and return $response->decoded_content;

  X::Ergatis::Submit->throw( $response );
}
